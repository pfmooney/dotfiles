" Preamble --------------------------------------------------------------- {{{

set nocompatible

filetype off

call plug#begin('~/.vim/plugged')

" Eye candy
Plug 'pfmooney/vim-pmolokai', { 'commit': 'c3a0477fd7ac91bd9a4983ba1f1f472315c05a10' }
Plug 'itchyny/lightline.vim', { 'commit': 'f7dd47eb55aed259cbc3e913f78c4ab21b3504bf' }

" Language support
Plug 'mmalecki/vim-node.js', { 'commit': '6f48575e748c22ccb3782bc02eb319b2b1b0f85d' }
Plug 'saltstack/salt-vim', { 'commit': '6ca9e3500cc39dd417b411435d58a1b720b331cc' }
Plug 'rust-lang/rust.vim', { 'commit': '8293adcd9c5645379133bea4d77de30b1476528c' }

" Utilties
Plug 'scrooloose/nerdtree', { 'commit': '72c3656799289d4635520c28e17f737066ce19d6', 'on':  'NERDTreeToggle' }
Plug 'tpope/vim-fugitive', { 'commit': '21ed53300186ae7cdfae8714fafe822097039bd7' }
Plug 'vim-utils/vim-man', { 'commit': 'cfdc78f52707b4df76cbe57552a7c8c28a390da4' }
Plug 'junegunn/fzf.vim', { 'commit': '467c3277884240f7b5430f8f4d600e3415c38f3b' }

" LSP
Plug 'prabirshrestha/async.vim', {'commit': 'f67ecb5a1120d0d0fb9312afcf262c829b218ca2' }
Plug 'prabirshrestha/vim-lsp', {'commit': '174e4bf82e87ebf9a9a76dfb3b19f05ac9044f07' }


call plug#end()
filetype plugin indent on

" }}}

" General Settings ------------------------------------------------------- {{{

set nobackup
set ruler
set nohlsearch
set bs=indent,eol,start
set nomodeline          " Disable modelines for security
set diffopt+=vertical   " Default to vsplits for vimdiff

set nosmartindent
set autoindent

set bg=dark
syntax on
set synmaxcol=800       " stop highlighting after 800 columns
set nu

" custom colors
colorscheme pmolokai

set list
set listchars=tab:\>\-
set fillchars=fold:\ ,vert:\|

set ttyfast
set lazyredraw
if &term =~ "xterm" || &term =~ "256color"
	" support 256 color terminals
	set t_Co=256
endif

" Draw highlight column at textwidth limit
if exists('+colorcolumn')
	set colorcolumn=+1
endif


" Better line-joining behavior (especially inside comments)
if v:version > 703
	set fo+=j
endif

let mapleader = ","
let maplocalleader = "\\"

set tabstop=4
set shiftwidth=4
set shiftround

nnoremap tl :tabnext<CR>
nnoremap th :tabprev<CR>
nnoremap tf :tabfirst<CR>
nnoremap tg :tablast<CR>
nnoremap tn :tabnew<CR>
nnoremap td :tabclose<CR>

" map space-bar to toggle folding
noremap <space> za

" customize summary for folded items
function! MyFoldText() " {{{
	let line = getline(v:foldstart)

	let nucolwidth = &fdc + &number * &numberwidth
	let windowwidth = winwidth(0) - nucolwidth
	let foldedlinecount = 1 + v:foldend - v:foldstart

	" expand tabs into spaces
	let onetab = strpart('          ', 0, &tabstop)
	let line = substitute(line, '\t', onetab, 'g')

	let linemsg = '…[' . foldedlinecount . ']'
	let linelimit = (windowwidth  > 80) ? (80 - len(linemsg)) : (windowwidth - len(linemsg))
	let line = strpart(line, 0, linelimit)
	return line . repeat(' ', 80 - len(line) - len(linemsg)) . linemsg
endfunction " }}}
set foldtext=MyFoldText()

" reset foldmethod and redraw screen when folding is hosed
nnoremap <C-L> :let &fdm = &fdm<CR>:redraw!<CR>

" highlight trailing spaces
augroup trailing
	au!
	au BufReadPre * :match ErrorMsg '\s\+$'
	au InsertEnter * :highlight clear ErrorMsg
	au InsertLeave * :highlight ErrorMsg term=reverse cterm=reverse ctermfg=124 guifg=White guibg=Red
augroup END

" Leave paste mode when leaving insert mode
autocmd InsertLeave * set nopaste

set wildmenu
set wildmode=list:longest

set wildignore+=.hg,.git,.svn                    " Version control
set wildignore+=*.aux,*.out,*.toc                " LaTeX intermediate files
set wildignore+=*.jpg,*.bmp,*.gif,*.png,*.jpeg   " binary images
set wildignore+=*.o,*.obj,*.exe,*.dll,*.manifest " compiled object files
set wildignore+=*.sw?                            " Vim swap files
set wildignore+=*.pyc                            " Python byte code
set wildignore+=*.orig                           " Merge resolution files

" Return to line conditional on g_file_line_return
fun! File_line_return()
	if exists('b:file_line_return') && b:file_line_return != 1
		return
	endif
	if line("'\"") > 0 && line("'\"") <= line("$") |
		execute 'normal! g`"zvzz' |
	endif
endfunction

" Make sure Vim returns to the same line when you reopen a file.
augroup line_return
	au!
	au BufReadPost * call File_line_return()
augroup END

" Support jsstyle/cstyle output
set efm+=%f:\ %l:%m

" ctags search
set tags=./tags;~/

" }}}

" Plugin Settings ------------------------------------------------------- {{{

" lightline {{{

" always show
set laststatus=2
" See statusline for lineinfo documentation
let g:lightline = {
	\ 'colorscheme': 'one',
	\ 'component': {
		\ 'lineinfo': '%3l:%-5(%c%V%)'
	\ },
	\ 'mode_map': {
		\ 'n' : 'NORM',
		\ 'i' : 'INS ',
		\ 'R' : 'REP ',
		\ 'v' : 'VIS ',
		\ 'V' : 'V-L ',
		\ "\<C-v>": 'V-B ',
		\ 'c' : 'COMM',
		\ 's' : 'SEL ',
		\ 'S' : 'S-L ',
		\ "\<C-s>": 'S-B ',
		\ 't': 'TERM',
	\ }
\ }

" }}}

" vim-lsp {{{

if exepath(expand('~/.cargo/bin/ra_lsp_server')) != ""
	au User lsp_setup call lsp#register_server({
			\ 'name': 'rust-analyzer',
			\ 'cmd': {server_info->[expand('~/.cargo/bin/ra_lsp_server')]},
			\ 'whitelist': ['rust'],
			\ })
endif

" vim-lsp has annoying behavior in files it shouldn't care about
let g:lsp_auto_enable = 0

let g:lsp_signs_error = {'text': '✗'}
let g:lsp_signs_warning = {'text': '!'}

function! s:on_lsp_buffer_enabled() abort
	setlocal omnifunc=lsp#complete
	setlocal signcolumn=auto
	nmap <buffer> <leader>ll <plug>(lsp-definition)
	nmap <buffer> <leader>ld <plug>(lsp-document-diagnostics)
	nmap <buffer> <leader>lr <plug>(lsp-rename)
	nmap <buffer> <leader>lh <plug>(lsp-hover)
	nmap <buffer> <leader>lf <plug>(lsp-document-format)
endfunction


augroup lsp_install
	au!
	" call s:on_lsp_buffer_enabled only for languages that has the server registered.
	autocmd User lsp_buffer_enabled call s:on_lsp_buffer_enabled()
augroup END


" }}}

" }}}

" Filetype-specific ------------------------------------------------------ {{{

" C {{{

augroup ft_c
	au!

	" Don't fold comments or '#if 0' blocks
	let c_no_comment_fold = 1
	let c_no_if0_fold = 1

	au FileType c setlocal foldmethod=syntax
	au FileType c setlocal list!
	" shiftround messes with block comments and illumos continuation style
	au FileType c setlocal noshiftround
	au FileType c setlocal ts=8 sw=8 list

augroup END

" }}}
"
" C++ {{{

augroup ft_cpp
	au!

	" Mirror settings for C
	au FileType cpp setlocal foldmethod=marker foldmarker={,}
	au FileType cpp setlocal list!
	au FileType cpp setlocal noshiftround
	au FileType cpp setlocal ts=8 sw=8 list

augroup END

" }}}

" Javascript {{{

augroup ft_javascript
	au!

	" Highlight json files as javascript
	au BufRead,BufNewFile *.json set filetype=javascript

	au Filetype javascript call JavaScriptFold()
	au Filetype javascript setlocal tabstop=2
	au Filetype javascript setlocal shiftwidth=2
	au Filetype javascript setlocal expandtab

augroup END

" }}}

" Git {{{

augroup ft_gitcommit
	au!

	" Don't bother with listchars in git commit messages
	au FileType gitcommit setlocal list!
	au FileType gitcommit setlocal spell
	" Start off on the first line
	au FileType gitcommit let b:file_line_return=0

augroup END

augroup ft_fugitive
	au!

	" Disable trailing space highlighting
	au FileType fugitive :match none
	" Don't bother with listchars in git commit messages

augroup END

" }}}

" Go {{{

augroup ft_go
	au!

	au FileType go setlocal foldmethod=syntax

augroup END

" }}}

" Mail {{{
augroup ft_mail
	au!

	" Enable line-folding (and don't highlight the trailing spaces)
	au FileType mail setlocal fo+=w
	au FileType mail :match none
	au FileType mail setlocal spell

augroup END

" }}}

" Makefile {{{

augroup ft_make
	au!

	" Several suffixes are used for Makefiles in illumos
	au BufRead,BufNewFile Makefile.* set filetype=make

	au FileType make setlocal ts=8

augroup END

" }}}

" Markdown {{{

	au BufRead,BufNewFile *.md set filetype=markdown
	au BufRead,BufNewFile *.restdown set filetype=markdown

" }}}

" muttrc {{{

augroup ft_muttrc
	au!

	au Filetype muttrc setlocal foldmethod=marker

augroup END

" }}}

" Puppet {{{

augroup ft_puppet
	au!

	au Filetype puppet setlocal foldmethod=marker
	au Filetype puppet setlocal foldmarker={,}
	" Conform to style guide for spaces/tabs
	au Filetype puppet setlocal tabstop=2
	au Filetype puppet setlocal shiftwidth=2
	au Filetype puppet setlocal expandtab
	" perform correct tabbing for => lists
	au Filetype puppet AddTabularPattern! block /^[^=]*\zs=>

	au Filetype puppet let g:syntastic_puppet_lint_arguments=
\'--no-class_parameter_defaults-check ' .
\'--no-documentation-check ' .
\'--no-autoloader_layout-check '
augroup END

" }}}

" Ruby {{{

augroup ft_ruby
	au!

	au Filetype ruby setlocal foldmethod=syntax
	" Conform to style guide for spaces/tabs
	au Filetype ruby setlocal tabstop=2
	au Filetype ruby setlocal shiftwidth=2
	au Filetype ruby setlocal expandtab

" }}}

" eRuby {{{

augroup ft_eruby
	au!

	au Filetype eruby setlocal foldmethod=syntax
	" Conform to style guide for spaces/tabs
	au Filetype eruby setlocal tabstop=2
	au Filetype eruby setlocal shiftwidth=2
	au Filetype eruby setlocal expandtab

" }}}

" rust {{{
augroup ft_rust
	au!

	au Filetype rust call lsp#enable()

" }}}

" Vim {{{

augroup ft_vim
	au!

	au FileType vim setlocal foldmethod=marker
	au FileType help setlocal textwidth=78
	au BufWinEnter *.txt if &ft == 'help' | wincmd L | endif

augroup END

" }}}

" }}}
